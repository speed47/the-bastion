#! /usr/bin/perl -T
# vim: set filetype=perl ts=4 sw=4 sts=4 et:
# NEEDGROUP osh-groupCreate
# SUDOERS %osh-groupCreate ALL=(root) NOPASSWD:/usr/bin/env perl -T /opt/bastion/bin/helper/osh-groupCreate *
# FILEMODE 0700
# FILEOWN 0 0

#>HEADER
use common::sense;
use Getopt::Long qw(:config no_auto_abbrev no_ignore_case);
use Term::ReadKey;

use File::Basename;
use lib dirname(__FILE__) . '/../../lib/perl';
use OVH::Bastion;
use OVH::Bastion::Plugin::groupSetRole;
use OVH::Bastion::Helper;

# Fetch command options
my $fnret;
my ($result, @optwarns);
my ($group, $owner, $algo, $size, $encrypted, $no_key, $comment);
eval {
    local $SIG{__WARN__} = sub { push @optwarns, shift };
    $result = GetOptions(
        "group=s"   => sub { $group     //= $_[1] },
        "owner=s"   => sub { $owner     //= $_[1] },
        "algo=s"    => sub { $algo      //= $_[1] },
        "size=i"    => sub { $size      //= $_[1] },
        "encrypted" => sub { $encrypted //= $_[1] },
        "no-key"    => sub { $no_key    //= $_[1] },
        "comment=s" => sub { $comment   //= $_[1] },
    );
};
if ($@) { die $@ }

if (!$result) {
    local $" = ", ";
    HEXIT('ERR_BAD_OPTIONS', msg => "Error parsing options: @optwarns");
}

OVH::Bastion::Helper::check_spurious_args();

if (!$group || !$owner) {
    HEXIT('ERR_MISSING_PARAMETER', msg => "Missing argument 'group' or 'owner'");
}
if ($no_key && ($algo || $size || $encrypted)) {
    EXIT('ERR_INVALID_PARAMETER', msg => "Can't specify 'no-key' along with 'algo', 'size' or 'encrypted'");
}
if (!$no_key && (!$algo || !$size)) {
    HEXIT('ERR_MISSING_PARAMETER', msg => "Missing argument 'algo' or 'size'");
}
if ($comment) {
    if ($comment =~ /^([a-zA-Z0-9=_,-]+)$/) {
        $comment = $1;    # untaint
    }
    else {
        HEXIT('ERR_INVALID_PARAMETER', msg => "Specified comment contains invalid characters");
    }
}

#<HEADER

if ($Self->isLocalRoot) {
    osh_debug "Real root, skipping checks of permissions";
}
else {
    if (!$Self->hasRole("groupCreate")) {
        HEXIT('ERR_SECURITY_VIOLATION', msg => "You're not allowed to run this, dear $Self");
    }
}

my $OwnerAccount = OVH::Bastion::Account->newFromName($owner, check => 1);
$OwnerAccount or HEXIT($OwnerAccount);

# group must pass sanity checks (name and such) but NOT exist already
my $Group = OVH::Bastion::Group->newFromName($group);
$Group or HEXIT($Group);

HEXIT('ERR_GROUP_ALREADY_EXISTS', msg => "The group '$group' already exits") if $Group->isExisting;

foreach my $test ($Group->sysGroup, $Group->sysGroupAclkeeper, $Group->sysGroupGatekeeper, $Group->sysGroupOwner) {
    $fnret = OVH::Bastion::sys_getgr_name(name => $test);
    $fnret and HEXIT('KO_ALREADY_EXISTING', msg => "The group $Group already exists");
}

if (OVH::Bastion::sys_getpw_name(name => $Group->sysUser)) {
    HEXIT('KO_ALREADY_EXISTING', msg => "The group $Group already exists");
}

#<PARAMS:GROUP

#>PARAMS:ALGO/SIZE
if (!$no_key) {
    $algo  = lc($algo);
    $fnret = OVH::Bastion::is_allowed_algo_and_size(algo => $algo, size => $size, way => 'egress');
    $fnret or HEXIT($fnret);

    # if we're still here, it's valid, untaint those
    ($algo) = $algo =~ m/(.+)/;
    ($size) = $size =~ m/(.+)/;
}

#<PARAMS:ALGO/SIZE

#>CODE
my $passphrase = '';    # empty by default
if ($encrypted) {
    print STDERR "Please enter a passphrase for the new group key (not echoed): ";
    ReadMode('noecho');
    chomp(my $pass1 = <STDIN>);
    if (length($pass1) < 5) {
        ReadMode('restore');
        HEXIT('ERR_PASSPHRASE_TOO_SMALL', msg => "Passphrase should have at least 5 chars");
    }
    print STDERR "\nPlease enter it again: ";
    chomp(my $pass2 = <STDIN>);
    print STDERR "\n";
    ReadMode('restore');
    if ($pass1 ne $pass2) {
        HEXIT('ERR_PASSPHRASE_MISMATCH', msg => "Passphrases don't match, please try again");
    }
    ($passphrase) = $pass1 =~ /(.+)/;    # untaint
}

# First create group
osh_info("Creating system groups...");
foreach my $tocreate ($Group->sysGroup, $Group->sysGroupOwner, $Group->sysGroupGatekeeper, $Group->sysGroupAclkeeper) {
    $fnret = OVH::Bastion::sys_groupadd(group => $tocreate, noisy_stderr => 1);
    if ($fnret->err ne 'OK') {
        HEXIT('ERR_GROUPADD_FAILED', msg => "Error while creating group '$tocreate' (" . $fnret->msg . ")");
    }
}

osh_debug("Creating directories");
if (!mkdir $Group->keyHome) {
    warn_syslog("Couldn't create key directory of $Group: $!, attempting to continue anyway");
}
chmod 0755, $Group->keyHome;

osh_info("Creating user corresponding to group $Group...");

# if a comment has been set, we'll store it as the user's GECOS corresponding to the group name
# user is member of the group, cannot login and have no password
$fnret = OVH::Bastion::sys_useradd(
    user => $Group->sysGroup,
    gid => $Group->sysGroup,
    shell => undef,
    comment => $comment,
    noisy_stderr => 1
);

if ($fnret->err ne 'OK') {
    HEXIT('ERR_USERADD_FAILED',
        msg => "Error while adding corresponding user of group $Group ($fnret)");
}

# Building /home/$sysGroup
OVH::Bastion::touch_file($Group->allowedIpFile);

$group = $Group->sysGroup;
osh_info("Adjusting permissions...");
my $bigX = (OVH::Bastion::is_linux() ? 'X' : 'x');
foreach my $command (
    ['chown', '-R',               "$group:$group",            "/home/$group"],
    ['chgrp', "$group-aclkeeper", "/home/$group/allowed.ip"],
    ['chmod', '-R',               "o-rwx,g=r$bigX,u=rw$bigX", "/home/$group"],
    ['chmod', '0664',             "/home/$group/allowed.ip"],
  )
{
    $fnret = OVH::Bastion::execute(cmd => $command, noisy_stderr => 1);
    $fnret->err eq 'OK'
      or HEXIT('ERR_CHMOD_FAILED', msg => "Error while running chmod to adjust permissions (" . $fnret->msg . ")");
}
chmod 0751, $Group->home if !OVH::Bastion::has_acls();

foreach my $gr ($Group->sysGroupOwner, $Group->sysGroupGatekeeper, $Group->sysGroupAclkeeper,
    "osh-whoHasAccessTo", "osh-auditor", "osh-superowner")
{
    OVH::Bastion::sys_setfacl(target => $Group->home, perms => "g:$gr:x")
      or HEXIT('ERR_SETFACL_FAILED', msg => "Error setting ACLs on group homedir");
}

# refresh the group, so that ->isExisting() succeeds, and that ->gid has a value
$Group->refresh();


my $keykeeper_uid = (getpwnam('keykeeper'))[2];
if (!chown $keykeeper_uid, $Group->gid, $Group->keyHome) {
    osh_warn("Couldn't chown the key directory ($!), will continue anyway...");
}


osh_debug("Adding allowkeeper to group $Group");
$fnret = OVH::Bastion::sys_addmembertogroup(group => $Group->sysGroup, user => 'allowkeeper');
$fnret or HEXIT($fnret);

osh_info("Adding $owner to proper system groups of $Group...");

# temporarily set ourselves owner manually so that we can add the wanted owner properly
# as owner/gatekeeper/member then revoke our own right
$fnret = OVH::Bastion::sys_addmembertogroup(group => $Group->sysGroupOwner, user => $self, noisy_stderr => 1);
$fnret or HEXIT($fnret);

# special case: if we're setting ourselves as owner, we must not remove
# our own rights after granting
my @todoList = (
    $owner eq $self
    ? (
        {action => 'add', role => 'owner',      account => $owner},
        {action => 'add', role => 'aclkeeper',  account => $owner},
        {action => 'add', role => 'gatekeeper', account => $owner},
        {action => 'add', role => 'member',     account => $owner},
      )
    : (
        {action => 'add', role => 'owner',      account => $owner},
        {action => 'add', role => 'aclkeeper',  account => $owner},
        {action => 'add', role => 'gatekeeper', account => $owner},
        {action => 'add', role => 'gatekeeper', account => $self},
        {action => 'add', role => 'member',     account => $owner},
        {action => 'del', role => 'gatekeeper', account => $self},
        {action => 'del', role => 'owner',      account => $self},
    )
);

foreach my $todo (@todoList) {
    $fnret = OVH::Bastion::Plugin::groupSetRole::act(
        Self           => $Self,
        account        => $todo->{'account'},
        Group          => $Group,
        action         => $todo->{'action'},
        role           => $todo->{'role'},
        silentoverride => 1
    );
    $fnret or HEXIT($fnret);
}

if (!$no_key) {
    osh_info("Generating main group key, this might take a few seconds...");

    $fnret = OVH::Bastion::generate_ssh_key(
        prefix         => $Group->name,
        folder         => $Group->keyHome,
        size           => $size,
        algo           => $algo,
        passphrase     => $passphrase,
        uid            => $keykeeper_uid,
        gid            => $Group->gid,
        group_readable => 1
    );
    $fnret or HEXIT($fnret);
}

# allowed to sudo for the group
osh_info("Configuring sudoers for this group");
$fnret = OVH::Bastion::execute(
    cmd          => [$OVH::Bastion::BASEPATH . '/bin/sudogen/generate-sudoers.sh', 'create', 'group', $Group->sysGroup],
    must_succeed => 1,
    noisy_stdout => 1
);
$fnret or HEXIT('ERR_CANNOT_CREATE_SUDOERS', msg => "An error occurred while creating sudoers for this group");

OVH::Bastion::syslog_formatted(
    severity => 'info',
    type     => 'group',
    fields   => [
        ['action',                   'create'],
        ['group',                    $Group->name],
        ['owner',                    $owner],
        ['egress_ssh_key_algorithm', $algo],
        ['egress_ssh_key_size',      $size],
        ['egress_ssh_key_encrypted', ($encrypted ? 'true' : 'false')],
    ]
);

# done at last!
HEXIT('OK', value => {group => $Group->name, owner => $owner});
