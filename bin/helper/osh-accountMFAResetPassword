#! /usr/bin/perl -T
# vim: set filetype=perl ts=4 sw=4 sts=4 et:
# NEEDGROUP osh-accountMFAResetPassword
# SUDOERS %osh-accountMFAResetPassword ALL=(root) NOPASSWD:/usr/bin/env perl -T /opt/bastion/bin/helper/osh-accountMFAResetPassword --account *
# FILEMODE 0700
# FILEOWN 0 0

#>HEADER
use common::sense;
use Getopt::Long qw(:config no_auto_abbrev no_ignore_case);

use File::Basename;
use lib dirname(__FILE__) . '/../../lib/perl';
use OVH::Result;
use OVH::Bastion;
use OVH::Bastion::Helper;

# Fetch command options
my $fnret;
my ($result, @optwarns);
my ($account);
eval {
    local $SIG{__WARN__} = sub { push @optwarns, shift };
    $result = GetOptions("account=s" => sub { $account //= $_[1] },);
};
if ($@) { die $@ }

if (!$result) {
    local $" = ", ";
    HEXIT('ERR_BAD_OPTIONS', msg => "Error parsing options: @optwarns");
}

OVH::Bastion::Helper::check_spurious_args();

if (not $account) {
    HEXIT('ERR_MISSING_PARAMETER', msg => "Missing argument 'account'");
}

#<HEADER

#>PARAMS:ACCOUNT
osh_debug("Checking account");
my $Account = OVH::Bastion::Account->newFromName($account, check => 1);
$Account or HEXIT($Account);
#<PARAMS:ACCOUNT

#>RIGHTSCHECK
if ($self eq 'root') {
    osh_debug "Real root, skipping checks of permissions";
}

# special case for self: if account==self, then is ok
elsif ($Self ne $Account) {
    $fnret = OVH::Bastion::is_user_in_group(user => $Self->sysUser, group => "osh-accountMFAResetPassword");
    if (!$fnret) {
        HEXIT('ERR_SECURITY_VIOLATION', msg => "You're not allowed to run this, dear $Self");
    }
}

#<RIGHTSCHECK

# don't allow a non-admin to reset the Password of an admin
if ($Account->isAdmin() && !$Self->isAdmin()) {
    HEXIT('ERR_SECURITY_VIOLATION', msg => "You can't reset the password of an admin without being admin yourself");
}

if (OVH::Bastion::is_user_in_group(user => $Account->sysUser, group => OVH::Bastion::MFA_PASSWORD_CONFIGURED_GROUP)) {
    $fnret = OVH::Bastion::sys_delmemberfromgroup(
        user  => $Account->sysUser,
        group => OVH::Bastion::MFA_PASSWORD_CONFIGURED_GROUP
    );
    $fnret or HEXIT($fnret);
}

$fnret = OVH::Bastion::sys_neutralizepassword(user => $Account->sysUser);
$fnret or HEXIT($fnret);

# remove expiration, or user could get locked out if s/he doesn't quickly set a new password,
# as the password expiration time is still taken into account even for '*' passwords
# 99999 is the /etc/shadow way to say "never" (273 years)
$fnret = OVH::Bastion::sys_setpasswordpolicy(user => $Account->sysUser, maxDays => 99999);
$fnret or HEXIT($fnret);

osh_info "Password has been reset, "
  . ($Account eq $Self ? 'you' : $Account)
  . " can setup a new password by using the `--osh selfMFASetupPassword' command, if applicable";
HEXIT('OK');
